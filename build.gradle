
subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'signing'

    group 'com.hczhang.hummingbird'
    version '0.2'

    ext.spring_version = '4.1.7.RELEASE'

    sourceCompatibility = 1.8

    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        // Metrics
        compile 'io.dropwizard.metrics:metrics-core:3.1.2'

        // Toolkits
        compile 'com.google.guava:guava:18.0'
        compile 'org.apache.commons:commons-lang3:3.3.2'

        // Log
        compile 'org.slf4j:slf4j-api:1.7.12'

        // Joda
        compile 'joda-time:joda-time:2.7'

        // Test lib
        testCompile 'org.testng:testng:6.9.4'
        testCompile 'org.mockito:mockito-core:1.9.5'
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives jar, javadocJar, sourcesJar
    }

    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment {
                    MavenDeployment deployment -> signing.signPom(deployment)
                }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'ostrich'
                    packaging 'jar'
                    // optionally artifactId can be defined here
                    description 'A simple cassandra util using DAO and Template pattern'
                    url 'https://github.com/steven-zhc/ostrich'

                    scm {
                        connection 'scm:git@github.com:steven-zhc/ostrich.git'
                        developerConnection 'scm:git@github.com:steven-zhc/ostrich.git'
                        url 'https://github.com/steven-zhc/ostrich.git'
                    }

                    licenses {
                        license {
                            name 'The Apache License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id 'steven.zhang'
                            name 'Haichao Zhang'
                            email 'steven.zhang031@gmail.com'
                        }
                    }
                }
            }
        }
    }
}

